
// module
module
  ::= use* domain_clause

qualifier
  ::= identifier ('.' qualifier)?

use
  ::= 'use' qualifier (use_decomposition | use_alias) ';'

use_alias
  ::= 'as' identifier

use_decomposition
  ::= ':' '{' use_decomposition_item (',' use_decomposition_item)* '}'

use_decomposition_item
  ::= qualifier use_decomposition?

// domain
enum
  ::= 'enum' identifier? ('of' domain_reference) '{' (enum_clause (',' enum_clause)*) '}'

enum_clause
  ::= (enum_inheritor | identifier) '=' expression

enum_inheritor
  ::= ':' qualifier

domain
  ::= 'domain' identifier generic_definition? ('of' domain_reference)? '{' domain_body '}'

domain_body ::=
  constructor_properties?
  use?
  (domain_trait | domain_constraint)*
  domain_clause*
  domain_getter*
  domain_method*
  constructor_method?

domain_trait
  ::= decorator? modifier? 'trait' identifier 'is' expression

domain_constraint
  ::= decorator? 'is' (identifier | callable | expression)

modifier
  ::= 'native' | 'abstract'

domain_modifier
  ::= decorator? (modifier | 'augment')

domain_clause
  ::= domain_modifier* (domain | enum)

domain_getter
  ::= decorator? modifier? 'get' functor

domain_method
  ::= decorator? modifier? identifier functor

constructor_properties
  ::= '{' (constructor_property (',' constructor_property)* ','?)? '}'

constructor_property
  ::= decorator? identifier ('?'? ':' type_reference ('=' expression)) | ('=' expression)

constructor_method
  ::= decorator? functor

// types
type_reference
  ::= domain_reference

// domain lookup
domain_reference
  ::= tuple_reference | (qualifier domain_reference_generic? ('[' ']')?)

tuple_reference
  ::= '[' (type_reference (',' type_reference)*)? ']'

domain_reference_generic
  ::= '<' type_reference (',' type_reference)* '>'

// generic definitions
generic_definition
  ::= '<' identifier (',' identifier)* ','? '>'

// functor
functor // !
  ::= (('(' functor_args? ')') | ('[' functor_args? ']')) ('?'? ':' type_reference)? '=>' block_expression

functor_args // !
  ::= functor_arg (',' functor_arg)*

functor_arg
  ::= identifier ':' type_reference ('=' expression)?

// expressions
expression // !
  ::= boolean

parenthesis
  ::= '(' expression ')'

chain
  ::= '.' identifier

indexed
  ::= '[' expression ']'

call
  ::= '(' call_args? ')'

call_args
  ::= call_arg (',' call_arg)* ','?

call_arg
  ::= (identifier ':')? expression

boolean // !
  ::= comparison (('&&' | '||' | '^^') expression)*

comparison // !
  ::= additive (('>' | '<' | '>=' | '<=' | '!=' | '==') expression)*

additive // !
  ::= multiplicative (('+' | '-') expression)*

multiplicative // !
  ::= numerative (('*' | '/' | '%' | '**') expression)*

numerative // !
  ::= applicative (('&' | '|' | '^' | '>>' | '<<') expression)*

applicative // !
  ::= unary is*

unary // !
  ::= unary_operator* unary_mutator? assignable unary_mutator? call*

unary_operator // !
  ::= '+' | '-' | '~' | '!' | 'typeof' | 'with'

unary_mutator // !
  ::= '--' | '++'

is // !
  ::= 'is' domain_reference

assignable
  ::= accessible (call? (chain | indexed))*

accessible
  ::= literal | object | array | match | callable | parenthesis | identifier

callable
  ::= functor

object
  ::= '{' (object_entry ',')* '}'

array
  ::= '[' (expression (',' expression)* ','?)? ']'

object_entry
  ::= identifier ':' expression

literal
  ::= string | number

// blocks / statements / expressions

block_expression
  ::= block | expression

block
  ::= '{' decorated_block_item* '}'

decorator
  ::= '@' expression

decorated_block_item
  ::= decorator? block_item

block_item
  ::= if | loop | label | ((break | return | statement) ';')

label
  ::= identifier ':'

statement
  ::= ((declaration | assignable) assignment_token)? expression

assignment_token
  ::= '=' | '+=' | '-=' | '/=' | '*=' | '%=' | '**=' | '|=' | '&=' | '^='

object_spread
  ::= '{' (object_spread_item (',' object_spread_item)*) '}'

object_spread_item
  ::= ('...'? identifier) | (identifier (':' object_spread)?)

declaration
  ::= 'let' dereference (':' type_reference)

dereference
  ::= identifier | object_spread

match // !
  ::= 'match' (expression) '=>' '{' match_statement+ default_match_statement? '}'

match_statement // !
  ::= 'case' expression ('with' dereference)? ':' (block | decorated_block_item)

default_match_statement // !
  ::= 'else' (block | decorated_block_item)

loop // !
  ::= 'each' loop_iterator block

if // !
  ::= 'if' statement block ('else' block)?

break
  ::= ('break' | 'continue') (('@' identifier) | expression)?

return
  ::= 'return' expression?

loop_iterator
  ::= declaration 'of' expression
